// [Xindage] i decided to set a single function for everything.
// Core of credits function
#define CREDIT_SET        0
#define CREDIT_ADD        1
#define CREDIT_SUB        2
#define CREDIT_REFUND     3

function void ModifyCredits (int player, int value, int mode, int showmsg)
{
	// Target activator if no player is set.
	if (player < 0) player = PlayerNumber();
	if (!PlayerInGame(player) || IsAdmin[player])
		return;

	int old = Credits[player];
	int new = old;

	// Apply operation
	if (mode == CREDIT_SET)
		new = value;
	else if (mode == CREDIT_ADD || mode == CREDIT_REFUND)
		new += value;
	else if (mode == CREDIT_SUB)
		new -= value;

	// Don't go past credit cap or negative
	int cap = GetConfig(CONFIG_CREDITCAP);
	if (cap && new > cap)	new = cap;
	if (new < 0)			new = 0;

	// If nothing changed do nothing.
	if (new == old)
		return;

	Credits[player] = new;
	SetActorInventory(3800 + player, "Credit", new);

	// Optional message
	if (!showmsg)
		return;

	if (PlayerNumber() != player)
		SetActivator(3800 + player);

	SetFont("SMALLFONT");

	if (mode == CREDIT_ADD)
	{
		IntelMessage(
			strparam(s:"You have been awarded with \cQ$\cD", d:value),
			false
		);
	}
	else if (mode == CREDIT_REFUND)
	{
		IntelMessage(
			strparam(s:"You have been refunded with \cQ$\cD", d:value),
			false
		);
	}
	else if (mode == CREDIT_SUB)
	{
		IntelMessage(
			strparam(s:"You have been deducted \cQ$\cD", d:value),
			false
		);
	}
	// Do CREDIT_SET if necessary.
}

//Used by Decorate
Script SC_GiveCredits (int amount) {GiveCredits (amount);}

function void GiveCredits (int amount)
{
	ModifyCredits(-1, amount, CREDIT_ADD, false);
}

function void GivePlayerCredits (int player, int amount)
{
	ModifyCredits(player, amount, CREDIT_ADD, false);
}

function void AwardCredits (int amount)
{
	ModifyCredits(-1, amount, CREDIT_ADD, true);
}

function void RefundCredits(int player, int amount)
{
	ModifyCredits(player, amount, CREDIT_REFUND, true);
}

function void TakeCredits(int player, int amount, int showmsg)
{
	ModifyCredits(player, amount, CREDIT_SUB, showmsg);
}

function void SetCredits(int player, int amount)
{
	ModifyCredits(player, amount, CREDIT_SET, false);
}

function void GiveTeamCredits(int blue, int red)
{
	for (int i = 0; i < MAXPLAYERS; i++)
	{
		if (!PlayerInGame(i) || IsAdmin[i])
			continue;

		if (GetPlayerInfo(i, PLAYERINFO_TEAM) == TEAM_BLUE)
			ModifyCredits(i, blue, CREDIT_ADD, false);
		else
			ModifyCredits(i, red, CREDIT_ADD, false);
	}
}

// MONEY RELATED SCRIPTS
function int GetCredits (void) {
	if (netstate == NETSTATE_Client)
		return CheckInventory ("Credit");
	return Credits[PlayerNumber()];
}

function int GetPlayerCredits (int i) {
	if (!PlayerInGame (i))
		return 0;
	
	if (netstate == NETSTATE_Client)
		return CheckActorInventory (3800+i, "Credit");
	return Credits[i];
}

function void SpendCredits (int amount) {
	int Discount = CalcDiscount (amount);
	int Final = amount - Discount;
	
	ModifyCredits(PlayerNumber(), final, CREDIT_SUB, false);	
	SetFont ("BIGFONT");
	if (Final > 0)
		HudMessage (s:"\cJYou have spent \cQ$\cD", d:amount, s:" \cF[\cQ-$\cD", d:Final, s:"\cF]";
			HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.5, 0.55, 2.0, 1.0);
	else
		HudMessage (s:"\cJYou have spent \cQ$\cD", d:amount;
			HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.5, 0.55, 2.0, 1.0);
}

function int CalcCreditFlow (int team) {
	int p = 25;
	if (GotResearch[team])
		p += 25;
	if (GotRefinery[team])
		p += 50;
	if (MiniRefinery[team])
		p += 75;
	
	int f = (GetCVAR("zeta_credflow") * p) / 100;
	if (!f)
		f = 1;
	
	return f;
}

// Credit flow
script SC_CREDITFLOW OPEN {
	while (1)
	{
		for (int i = 0; i < MAXPLAYERS; i++)
		{
			if (!PlayerInGame (i) || GetActorHealth (3800 + i) <= 0)
				continue;
				
			// [JD] Simplified this
			GivePlayerCredits(i, CalcCreditFlow(GetPlayerInfo(i, PLAYERINFO_TEAM)));
		}
		
		delay (35);
	}
}

//Price check
script 330 (int amount) {
	int d = CalcDiscount (amount);
	amount -= d;
	
	if (d != 0) {
		SetFont ("BIGFONT");
		HudMessage (s:"\cJDiscount \cQ-$\cD", d:d, s:"\cF = \cQ$\cD", d:amount;
			HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.25, 0.1, 2.0, 1.0);
	}
	
	SetResultValue (amount);
}

// CREDIT BONUS
// [Xindage] Added an extra of 50 each loop to make the bonus more valuable as the game goes.
int Extrabonusloop = 0;

Script SC_CREDITBONUS OPEN
{
	while (GetCVAR("zeta_bonustime") <= 0)	delay (5 * 35);
	delay (GetCVAR("zeta_bonustime") * MINUTETICS);
	
	int Value = GetCVAR("zeta_bonusamount") + (Extrabonusloop * 50);
	
	GiveTeamCredits(Value, Value);
	AmbientSound ("powerup/credsound", 127);
	SetFont ("SMALLFONT");
	IntelMessage (strparam (s:"Credit bonus! You all get \cQ$\cD", d:Value, s:"\cJ!"), true);
	Extrabonusloop++;
	restart;
}

function int CalcDiscount (int amount) {
	// Privates don't get discounts
	if (GetRank () == RANK_PRIVATE || amount <= 0)
		return 0;
	
	int d;
	if (amount < 200 && amount > 0)
		d = 10;
	else if (amount >= 200 && amount < 400)
		d = 15;
	else if (amount >= 400 && amount < 800)
		d = 30;
	else if (amount >= 800 && amount < 1600)
		d = 50;
	else if (amount > 1600)
		d = 75;
	
	if (GetRank () >= RANK_MAJOR)
		d *= 4;
	else if (GetRank () >= RANK_LIEUTENANT)
		d *= 3;
	else if (GetRank () >= RANK_MASTERSGT)
		d *= 2;
	
	return d;
}
