/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * All Out War 2 death script
 * Credit to VoltlocK for originally creating the engine
 * With modifications by the Omega Team
 *
 * You may use portions of this script in your project as long as you give
 * credit where credit is due. Please don't be lame and just copy-paste any
 * of this and call it your own. Thanks!
 */

// Death script
// [Xindage] 2023/11/26 - There's lot of unused data that was cleaned.
// If necessary look for the commit at that date for detailed changes.
script SC_DEATH DEATH
{
	// Killer information
	int killer_n = GetTargetInfo (TARGETINFO_PlayerNum);
	int killer_t = GetTargetInfo (TARGETINFO_Team);
	int killer_i = GetTargetInfo (TARGETINFO_TID);

	// Victim information
	int victim_n = PlayerNumber();
	int victim_t = PlayerTeam();
	int threat_level = 0;

	Thing_ChangeTID (0, TID_DeadPlayer);
	TakeInventory ("ThermalCloakingTime", 90);
	SetInventory ("InMenu", 0);
	SetPlayerProperty (0, 0, PROP_FROZEN);
	SetPlayerProperty (0, 0, PROP_TOTALLYFROZEN);
	
	// Tiberium dropped by dead harvesters
	if (CheckInventory ("TiberiumCrystal") && !CheckInventory ("IsInSpawnRoom"))
		SpawnSpotForced ("TiberiumCrystalDropped", 0, 0, 0);
	
	// Bomb-pack disarm sound
	if (CheckWeapon ("BombPack") && !CheckInventory ("IsTerrorist"))
		ActivatorSound ("c4/disarm", 127);
	
	// Activate Plasma Cannon cooldown, if player had one
	if (CheckInventory ("PlasmaCannon"))
		PlasmaCooldown[PlayerNumber()] = Timer() + (GetCVAR("zeta_plasmacooldown") * MINUTETICS);
	
	// How much is this thing worth?
	// Xindage 2022/10/17 - three mechs was missing from the list: Mastodon, tortoise and lancer.
	// Mech points is also now based on their weight.
	int points = 1;
	bool VictimWasMech = false; // Used for credits calculation.
	if (CheckInventory ("IsMech"))
	{
		VictimWasMech = true;
		// Light
		if (CheckInventory ("OrcaWeapons") ||
			CheckInventory ("RavenWeapons") ||
			CheckInventory ("MastodonWeapons"))
			{points = 3;
			threat_level = 2;}
		// Medium
		if (CheckInventory ("TortoiseWeapons") ||
			CheckInventory ("TitanWeapons") ||
			CheckInventory ("WolverineWeapons"))
			{points = 4;
			threat_level = 4;}
		// Heavy
		if (CheckInventory ("MadCatWeapons") ||
			CheckInventory ("LancerWeapons") ||
			CheckInventory ("JuggernautWeapons"))
			{points = 5;
			threat_level = 6;}
	}
	
	// If we're in alternate experience mode, we lose all experience now
	if (GetConfig (CONFIG_ALTEXP))
		SetExperience (0);
	
	// The victim is now allowed to request killer's health
	CanCheckKillerHealth[PlayerNumber()] = true;
	
	// [Xindage] I'm not the one who create it but i'll comment what it does.
	// Every 75th death is going to be extreme for whatever reason.
	deathtoll++;
	if (deathtoll % 75 == (75 - 1))
		SetActorState (0, "XDeath");
	
	// If the victim was a suicide bomber and was killed at close range with a
	// melee weapon, the killer gets the bomb pack.
	if (GetClass () == CLASS_SuicideBomber &&
		AproxDistance (0, killer_i) < 80.0 &&
			(
				CheckTargetWeapon ("Unarmed") ||
				CheckTargetWeapon ("EagerBeaver") ||
				CheckTargetWeapon ("Knife") ||
				CheckTargetWeapon ("Lightsaber")
			)
		)
	{
		GiveToTarget ("BombPackItem", 1);
		TargetMessage ("You got the bomb pack! Terrorist!");
		ThingSound (3800 + killer_i, "terrorist/activate", 127);
	}
	
	bool IsInEnemyBase = SC_DISTCHECK(0, !PlayerTeam(), MAX_TURRET_DISTANCE/2, 0);
	if (!intHolder[i_SuddenDeath]) // No need to calculate things in sudden death.
	{
		bool IsInBase = SC_DISTCHECK(0, PlayerTeam(), MAX_TURRET_DISTANCE, 0);
		// Check if the death happens in your own base, we dont want to defensive use of these class to be an issue.
		if (!IsInBase)
		{
			// Mech in base has reduced effect as can be used in defense, free for light
			if (VictimWasMech) threat_level -= 2;
			// Calculate the threat level of the killed player
			if (checkInventory("TimedC4Count") >= 2) threat_level += checkInventory("TimedC4Count")-1;
			if (checkInventory("IsStealth")) threat_level += 1;
			if (checkInventory("RepairGun") || checkInventory("HealGun") || checkInventory("BombSquadGun")) threat_level += 1;
			if (checkInventory("PlasmaCannon")) threat_level += 2;
			if (checkInventory("UtilityGun")) threat_level += 2;
			if (checkInventory("TimeGun")) threat_level += 2;
			if (checkInventory("RemoteC4")) threat_level += 3;
			if (checkInventory("BombPack")) threat_level += 3;
			if (checkInventory("IsKamikaze")) threat_level += 3;
		}
		// Nuke is still an offensive only item.
		if (checkInventory("IonCannonBeacon") || checkInventory("NuclearStrikeBeacon")) threat_level += 3;
		
		// =====
		// Battery Charge award.
		// Check if the death happens in the enemy base area
		int battery_gain = 60 * threat_level;	// Base reward.
			
		if (IsInEnemyBase)
			battery_gain = (battery_gain * 3) / 2;	// Defensive kill, 50% bonus energy.
		if (battery_gain > 0) // Reverse team is used to prevent suicides.
			BatteryModify (!PlayerTeam(), battery_gain, BATTERY_ADD);
	}
	
	// =========================================================================
	// KILLER POV STARTS HERE
	
	SetActivatorToTarget (0);
	
	// Remove tickets from the victim team.
	TakeTickets (victim_t, points);
	
	if (PlayerNumber() != -1) {
		// If the kill was done with a blood revolver, give a new clip.
		if (!CheckInventory ("BloodRevolverKill") && CheckWeapon ("BloodRevolver"))
			GiveInventory ("BloodRevolverAmmo", 1);
		
		int extra = 1.0;
		
		// No points for suicides
		if (victim_n != killer_n && killer_n != -1)
		{
			LocalAmbientSound ("game/boink", 127);

			// Give 50% bonus to all if the kill was a defensive one.
			if (IsInEnemyBase) extra = 1.5;
			
			// Rewards
			GivePoints ((2 * points * extra) >> 16 );
			AddExperience((50 * extra * points) >> 16 );
			// Mech Kill
			if (VictimWasMech)	ModifyCredits(PlayerNumber(), ((GetCVAR("zeta_creditfrag") * extra * points * 3) >> 16 ), CREDIT_ADD, true);
			// infantry Kill
			else				ModifyCredits(PlayerNumber(), ((GetCVAR("zeta_creditfrag") * extra) >> 16 ), CREDIT_ADD, false);
			
			delay (5);
		}
	}
}

// Client-side death script
script SC_CL_PLAYERDEATH DEATH CLIENTSIDE {
	// If we want our killer's health, request it now
	int killernum = GetTargetInfo (TARGETINFO_PlayerNum);
	if (PlayerNumber () == intHolder[i_consoleplayer] &&
		killernum != intHolder[i_consoleplayer] &&
		GetCVar ("zeta_cl_killerhealth"))
	{
		RequestScriptPuke(919, 0, 0, 0);
	}
	
	// [Dusk] Mark down if we died.
	if (PlayerNumber() == intHolder[i_consoleplayer])
		intHolder[i_consoledead] = true;
}

// Juggernaut stomp health
script SC_JUGGERNAUTSTOMP (void) {
	SetActivatorToTarget (0);
	
	if (!CheckWeapon ("JuggernautWeapons"))
		terminate;
	
	SetHealth (GetHealth () + 100);
	if (GetHealth () > GetSpawnHealth ())
		SetHealth (GetSpawnHealth ());
}

// [SP] Fragging a monster
Script SC_MONSTERDEATH (int Amount) {
	if (PlayerNumber() != -1) {
		LocalAmbientSound ("game/boink", 127);
		AddExperienceWithMessage (Amount);
		GiveCredits ((3 * Amount) / 2);
	}
}
