// Razor wire
actor RazorWire {
	//$Category Objects
	+SHOOTABLE
	+NOBLOOD
	+NODAMAGETHRUST
	Health 400
	Radius 32
	Height 32
	Mass 0x7FFFFFFF
	Scale 0.25
	RenderStyle translucent
	Alpha 0.0
	Obituary "%o walked into razor wire."
	DamageFactor "Disarm", 0.0
	DamageFactor "Mechstomp", 0.0
	DamageFactor "StealthKnife", 1.0
	Tag "Razor wire (red)"
	states {
	Spawn:
		WIRE AA 0 A_CheckSight("Death.Deconstruction")
		WIRE AAAAAAAAAA 3 A_FadeIn (0.1)
		WIRE A 0 A_ChangeFlag ("NOCLIP", 0)
		WIRE A 35 A_Gravity
		goto Idle
	Idle:
		WIRE A 18 ACS_ExecuteAlways (983, 0, TEAM_RED)
		wait
	Death:  
		TNT1 A 0 
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain", 0, 0, 32)
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 4
		stop
	Death.StealthKnife:
	Death.Deconstruction:
		"----" A 0 A_NoBlocking
		"----" A 2 A_PlaySound ("utility/deconstruct")
		"----" AAAAAAAAAA 2 A_Fadeout(0.1) 
		stop
	}
}

actor RazorWireBlue : RazorWire {
	Tag "Razor wire (blue)"
	States {
	Spawn:
		WIRE AA 0 A_CheckSight("Death.Deconstruction")
		WIRE AAAAAAAAAA 3 A_FadeIn (0.1)
		WIRE A 0 A_ChangeFlag ("NOCLIP", 0)
		WIRE A 35 A_Gravity
		goto Idle
	Idle:
		WIRE A 18 ACS_ExecuteAlways (983, 0, TEAM_BLUE)
		wait
	}
}

actor RazorWireMap : RazorWire 21152 {
	-NOCLIP
	+NODAMAGE
	RenderStyle normal
	Alpha 1.0
	tag "Razor wire"
	states {
	Spawn:
		WIRE A 18 ACS_ExecuteAlways (983, 0, 255)
		wait
	}
}

actor PowerRazorWire : PowerSpeed {speed 0.5}
actor RazorWireSlow : PowerupGiver {
	inventory.maxamount 0
	powerup.type "RazorWire"
	powerup.duration 35
	+AUTOACTIVATE
}